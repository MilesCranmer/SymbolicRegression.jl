<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Custom Types · SymbolicRegression.jl</title><script data-outdated-warner src="../../assets/warner.js"></script><link rel="canonical" href="https://ai.damtp.cam.ac.uk/symbolicregression/stable/examples/custom_types/"/><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.045/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.24/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><a class="docs-logo" href="../../index_base/"><img src="../../assets/logo.svg" alt="SymbolicRegression.jl logo"/></a><div class="docs-package-name"><span class="docs-autofit"><a href="../../index_base/">SymbolicRegression.jl</a></span></div><form class="docs-search" action="../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../index_base/">Contents</a></li><li><a class="tocitem" href="../../">Home</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../">Short Examples</a></li><li><a class="tocitem" href="../template_expression/">Template Expressions</a></li><li><a class="tocitem" href="../parameterized_function/">Parameterized Expressions</a></li><li><a class="tocitem" href="../template_parametric_expression/">Parameterized Template Expressions</a></li><li class="is-active"><a class="tocitem" href>Custom Types</a></li><li><a class="tocitem" href="../../slurm/">Using SymbolicRegression.jl on a Cluster</a></li></ul></li><li><a class="tocitem" href="../../api/">API</a></li><li><a class="tocitem" href="../../losses/">Losses</a></li><li><a class="tocitem" href="../../types/">Types</a></li><li><a class="tocitem" href="../../customization/">Customization</a></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>Custom Types</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Custom Types</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/MilesCranmer/SymbolicRegression.jl/blob/master/examples/custom_types.jl" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Working-with-Custom-Input-Types"><a class="docs-heading-anchor" href="#Working-with-Custom-Input-Types">Working with Custom Input Types</a><a id="Working-with-Custom-Input-Types-1"></a><a class="docs-heading-anchor-permalink" href="#Working-with-Custom-Input-Types" title="Permalink"></a></h1><p>Say that you have some custom input type you want to evolve an expression for. It doesn&#39;t even need to be a numerical type. It could be anything –- even a string!</p><p>Let&#39;s actually try this. Let&#39;s evolve an <em>expression over strings</em>.</p><p>First, we mock up a dataset. Say that we wish to find the expression</p><p class="math-container">\[y = \text{zip}(
    \text{concat}(x_1, \text{concat}(\text{``abc&#39;&#39;}, x_2)),
    \text{concat}(
        \text{concat}(\text{tail}(x_3), \text{reverse}(x_4)),
        \text{``xyz&#39;&#39;}
    )
)\]</p><p>We will define some unary and binary operators on strings:</p><pre><code class="language-julia hljs">using SymbolicRegression
using DynamicExpressions: GenericOperatorEnum
using MLJBase: machine, fit!, report, MLJBase
using Random

&quot;&quot;&quot;Returns the first half of the string.&quot;&quot;&quot;
head(s::String) = length(s) == 0 ? &quot;&quot; : join(collect(s)[1:max(1, div(length(s), 2))])

&quot;&quot;&quot;Returns the second half of the string.&quot;&quot;&quot;
tail(s::String) = length(s) == 0 ? &quot;&quot; : join(collect(s)[max(1, div(length(s), 2) + 1):end])

&quot;&quot;&quot;Concatenates two strings.&quot;&quot;&quot;
concat(a::String, b::String) = a * b

&quot;&quot;&quot;Interleaves characters from two strings.&quot;&quot;&quot;
function zip(a::String, b::String)
    total_length = length(a) + length(b)
    result = Vector{Char}(undef, total_length)
    i_a = firstindex(a)
    i_b = firstindex(b)
    i = firstindex(result)
    while i &lt;= total_length
        if i_a &lt;= lastindex(a)
            result[i] = a[i_a]
            i += 1
            i_a = nextind(a, i_a)
        end
        if i_b &lt;= lastindex(b)
            result[i] = b[i_b]
            i += 1
            i_b = nextind(b, i_b)
        end
    end
    return join(result)
end</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">Main.zip</code></pre><p>Now, let&#39;s use these operators to create a dataset.</p><pre><code class="language-julia hljs">function single_instance(rng=Random.default_rng())
    x_1 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))
    x_2 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))
    x_3 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))
    x_4 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))

    # True formula:
    y = zip(x_1 * &quot;abc&quot; * x_2, tail(x_3) * reverse(x_4) * &quot;xyz&quot;)
    return (; X=(; x_1, x_2, x_3, x_4), y)
end

dataset = let rng = Random.MersenneTwister(0)
    [single_instance(rng) for _ in 1:128]
end</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">128-element Vector{@NamedTuple{X::@NamedTuple{x_1::String, x_2::String, x_3::String, x_4::String}, y::String}}:
 (X = (x_1 = &quot;tn&quot;, x_2 = &quot;k&quot;, x_3 = &quot;cxrnis&quot;, x_4 = &quot;lglpjkcz&quot;), y = &quot;tnniasbzcckkjplglxyz&quot;)
 (X = (x_1 = &quot;hfjeyytky&quot;, x_2 = &quot;qjcbvvyj&quot;, x_3 = &quot;wjhtddt&quot;, x_4 = &quot;oqj&quot;), y = &quot;htfdjdetyjyqtokxyyazbcqjcbvvyj&quot;)
 (X = (x_1 = &quot;pi&quot;, x_2 = &quot;gipdeg&quot;, x_3 = &quot;dvjqjyz&quot;, x_4 = &quot;sgcfwiovcz&quot;), y = &quot;pqijaybzczgcivpodiewgfcgsxyz&quot;)
 (X = (x_1 = &quot;mgvyyorefh&quot;, x_2 = &quot;gkklo&quot;, x_3 = &quot;bggro&quot;, x_4 = &quot;rzsigzr&quot;), y = &quot;mggrvoyryzogriesfzhraxbyczgkklo&quot;)
 (X = (x_1 = &quot;e&quot;, x_2 = &quot;lmuj&quot;, x_3 = &quot;afcwnxq&quot;, x_4 = &quot;m&quot;), y = &quot;ewanbxcqlmmxuyjz&quot;)
 (X = (x_1 = &quot;octxy&quot;, x_2 = &quot;ynvxwmvtvy&quot;, x_3 = &quot;bodmeh&quot;, x_4 = &quot;wwmg&quot;), y = &quot;omcethxgymawbwcxyynzvxwmvtvy&quot;)
 (X = (x_1 = &quot;rgff&quot;, x_2 = &quot;eiovcljep&quot;, x_3 = &quot;ir&quot;, x_4 = &quot;lgcpnszlm&quot;), y = &quot;rrgmflfzasbncpecigolvxcylzjep&quot;)
 (X = (x_1 = &quot;xo&quot;, x_2 = &quot;ga&quot;, x_3 = &quot;ohhj&quot;, x_4 = &quot;iq&quot;), y = &quot;xhojaqbicxgyaz&quot;)
 (X = (x_1 = &quot;lhh&quot;, x_2 = &quot;bnfzulvnv&quot;, x_3 = &quot;edf&quot;, x_4 = &quot;jhsnuggpw&quot;), y = &quot;ldhfhwapbgcgbunnfszhujlxvynzv&quot;)
 (X = (x_1 = &quot;cqguvcz&quot;, x_2 = &quot;grxisqpdp&quot;, x_3 = &quot;kknh&quot;, x_4 = &quot;wz&quot;), y = &quot;cnqhgzuwvxcyzzabcgrxisqpdp&quot;)
 ⋮
 (X = (x_1 = &quot;ucpkqr&quot;, x_2 = &quot;ov&quot;, x_3 = &quot;w&quot;, x_4 = &quot;heibls&quot;), y = &quot;uwcsplkbqireahbxcyozv&quot;)
 (X = (x_1 = &quot;dbysddes&quot;, x_2 = &quot;es&quot;, x_3 = &quot;quewtprhi&quot;, x_4 = &quot;qhflrx&quot;), y = &quot;dtbpyrshdidxerslafbhcqexsyz&quot;)
 (X = (x_1 = &quot;ciroafn&quot;, x_2 = &quot;q&quot;, x_3 = &quot;nm&quot;, x_4 = &quot;yzdohxu&quot;), y = &quot;cmiurxohaofdnzaybxcyqz&quot;)
 (X = (x_1 = &quot;sjgyhmxw&quot;, x_2 = &quot;abmanioxss&quot;, x_3 = &quot;r&quot;, x_4 = &quot;urhjxod&quot;), y = &quot;srjdgoyxhjmhxrwuaxbyczabmanioxss&quot;)
 (X = (x_1 = &quot;howknyc&quot;, x_2 = &quot;gwqujdkz&quot;, x_3 = &quot;gvaax&quot;, x_4 = &quot;rmsrsabu&quot;), y = &quot;haoawxkunbyacsarbscmgrwxqyuzjdkz&quot;)
 (X = (x_1 = &quot;hk&quot;, x_2 = &quot;gpixnpb&quot;, x_3 = &quot;hlpfcdvmka&quot;, x_4 = &quot;vc&quot;), y = &quot;hdkvambkcagcpvixxynzpb&quot;)
 (X = (x_1 = &quot;ljgcmeohrq&quot;, x_2 = &quot;wssrnkl&quot;, x_3 = &quot;dno&quot;, x_4 = &quot;plwqqjlr&quot;), y = &quot;lnjogrclmjeqoqhwrlqpaxbyczwssrnkl&quot;)
 (X = (x_1 = &quot;wfojulfiu&quot;, x_2 = &quot;eozo&quot;, x_3 = &quot;tujarnsf&quot;, x_4 = &quot;gvgwlym&quot;), y = &quot;wrfnosjfumlyfliwugavbgcxeyozzo&quot;)
 (X = (x_1 = &quot;ztupcehmpv&quot;, x_2 = &quot;nrpd&quot;, x_3 = &quot;mnarvbw&quot;, x_4 = &quot;hrkvfto&quot;), y = &quot;zrtvubpwcoethfmvpkvrahbxcynzrpd&quot;)</code></pre><p>We&#39;ll get them in the right format for MLJ:</p><pre><code class="language-julia hljs">X = [d.X for d in dataset]
y = [d.y for d in dataset];</code></pre><p>To actually get this working with SymbolicRegression, there are some key functions we will need to overload.</p><p>First, we say that a single string is one &quot;scalar&quot; constant:</p><pre><code class="language-julia hljs">import DynamicExpressions: count_scalar_constants
count_scalar_constants(::String) = 1</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">count_scalar_constants (generic function with 7 methods)</code></pre><p>Next, we define an initializer (which is normally 0.0 for numeric types).</p><pre><code class="language-julia hljs">import SymbolicRegression: init_value
init_value(::Type{String}) = &quot;&quot;</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">init_value (generic function with 3 methods)</code></pre><p>Next, we define a random sampler. This is only used for generating initial random leafs; the <code>mutate_value</code> function is used for mutating them and moving around in the search space.</p><pre><code class="language-julia hljs">using Random: AbstractRNG
import SymbolicRegression: sample_value
sample_value(rng::AbstractRNG, ::Type{String}, _) = join(rand(rng, &#39;a&#39;:&#39;z&#39;) for _ in 1:rand(rng, 0:5))</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">sample_value (generic function with 3 methods)</code></pre><p>We also define a pretty printer for strings, so it is easier to tell apart variables and operators from string constants.</p><pre><code class="language-julia hljs">import SymbolicRegression.InterfaceDynamicExpressionsModule: string_constant
function string_constant(val::String, ::Val{precision}, _) where {precision}
    val = replace(val, &quot;\&quot;&quot; =&gt; &quot;\\\&quot;&quot;, &quot;\\&quot; =&gt; &quot;\\\\&quot;)
    return &#39;&quot;&#39; * val * &#39;&quot;&#39;
end</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">string_constant (generic function with 2 methods)</code></pre><p>We also disable constant optimization for strings, since it is not really defined. If you have a type that you do want to optimize, you should follow the <code>DynamicExpressions</code> value interface and define the <code>get_scalar_constants</code> and <code>set_scalar_constants!</code> functions.</p><pre><code class="language-julia hljs">import SymbolicRegression.ConstantOptimizationModule: can_optimize
can_optimize(::Type{String}, _) = false</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">can_optimize (generic function with 3 methods)</code></pre><p>Finally, the most complicated overload for <code>String</code> is <code>mutate_value</code>, which we need to define so that any constant value can be iteratively mutated into any other constant value.</p><p>We also typically want this to depend on the temperature –- lower temperatures mean a smaller rate of change. You can use temperature as you see fit, or ignore it.</p><pre><code class="language-julia hljs">using SymbolicRegression.UtilsModule: poisson_sample

import SymbolicRegression: mutate_value

sample_alphabet(rng::AbstractRNG) = rand(rng, &#39;a&#39;:&#39;z&#39;)

function mutate_value(rng::AbstractRNG, val::String, T, options)
    max_length = 10
    lambda_max = 5.0
    λ = max(nextfloat(0.0), lambda_max * clamp(float(T), 0, 1))
    n_edits = clamp(poisson_sample(rng, λ), 0, 10)
    chars = collect(val)
    ops = rand(rng, (:insert, :delete, :replace, :swap), n_edits)
    for op in ops
        if op == :insert
            insert!(chars, rand(rng, 0:length(chars)) + 1, sample_alphabet(rng))
        elseif op == :delete &amp;&amp; !isempty(chars)
            deleteat!(chars, rand(rng, eachindex(chars)))
        elseif op == :replace
            if isempty(chars)
                push!(chars, sample_alphabet(rng))
            else
                chars[rand(rng, eachindex(chars))] = sample_alphabet(rng)
            end
        elseif op == :swap &amp;&amp; length(chars) &gt;= 2
            i = rand(rng, 1:(length(chars) - 1))
            chars[i], chars[i + 1] = chars[i + 1], chars[i]
        end
        if length(chars) &gt; max_length
            chars = chars[1:max_length]
        end
    end
    return String(chars[1:min(end, max_length)])
end</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">mutate_value (generic function with 3 methods)</code></pre><p>This concludes the custom type interface. Now let&#39;s actually use it!</p><p>For the loss function, we will use Levenshtein edit distance. This lets the evolutionary algorithm gradually change the strings into the desired output.</p><pre><code class="language-julia hljs">function edit_distance(a::String, b::String)::Float64
    a, b = length(a) &gt;= length(b) ? (a, b) : (b, a)  ## Want shorter string to be b
    a, b = collect(a), collect(b)  ## Convert to vectors for uniform indexing
    m, n = length(a), length(b)

    m == 0 &amp;&amp; return n
    n == 0 &amp;&amp; return m
    a == b &amp;&amp; return 0

    # Initialize the previous row (distances from empty string to b[1:j])
    prev = collect(0:n)
    curr = similar(prev)

    for i_a in 1:m
        curr[1] = i_a
        ai = a[i_a]
        for i_b in 1:n
            cost = ai == b[i_b] ? 0 : 1
            curr[i_b + 1] = min(prev[i_b + 1] + 1, curr[i_b] + 1, prev[i_b] + cost)
        end
        prev, curr = curr, prev
    end

    return Float64(prev[n + 1])  ## Make sure to convert to your `loss_type`!
end</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">edit_distance (generic function with 1 method)</code></pre><p>Next, let&#39;s create our regressor object. We pass <code>binary_operators</code> and <code>unary_operators</code> as normal, but now we also pass <code>GenericOperatorEnum</code>, because we are dealing with non-numeric types.</p><p>We also need to manually define the <code>loss_type</code>, since it&#39;s not inferrable from <code>loss_type</code>.</p><pre><code class="language-julia hljs">binary_operators = (concat, zip)
unary_operators = (head, tail, reverse)
hparams = (;
    batching=true,
    batch_size=32,
    maxsize=20,
    parsimony=0.1,
    adaptive_parsimony_scaling=20.0,
    mutation_weights=MutationWeights(; mutate_constant=1.0),
    early_stop_condition=(l, c) -&gt; l &lt; 1.0 &amp;&amp; c &lt;= 15,  # src
)
model = SRRegressor(;
    binary_operators,
    unary_operators,
    operator_enum_constructor=GenericOperatorEnum,
    elementwise_loss=edit_distance,
    loss_type=Float64,
    hparams...,
);

mach = machine(model, X, y; scitype_check_level=0)</code></pre><pre class="documenter-example-output"><code class="nohighlight hljs ansi">untrained Machine; caches model-specific representations of data
  model: SRRegressor(defaults = nothing, …)
  args: 
    1:	Source @071 ⏎ ScientificTypesBase.Table{AbstractVector{ScientificTypesBase.Textual}}
    2:	Source @209 ⏎ AbstractVector{ScientificTypesBase.Textual}
</code></pre><p>At this point, you would run <code>fit!(mach)</code> as usual. Ignore the MLJ warnings about <code>scitype</code>s.</p><pre><code class="language-julia hljs">fit!(mach)</code></pre><hr/><details>
<summary> Show raw source code </summary><pre><code class="language-julia hljs">#=
# Working with Custom Input Types

Say that you have some custom input type you want to evolve an expression for.
It doesn&#39;t even need to be a numerical type. It could be anything --- even a string!

Let&#39;s actually try this. Let&#39;s evolve an _expression over strings_.

First, we mock up a dataset. Say that we wish to find the expression

\```math
y = \text{zip}(
    \text{concat}(x_1, \text{concat}(\text{``abc&#39;&#39;}, x_2)),
    \text{concat}(
        \text{concat}(\text{tail}(x_3), \text{reverse}(x_4)),
        \text{``xyz&#39;&#39;}
    )
)
\```

We will define some unary and binary operators on strings:
=#

using SymbolicRegression
using DynamicExpressions: GenericOperatorEnum
using MLJBase: machine, fit!, report, MLJBase
using Random

&quot;&quot;&quot;Returns the first half of the string.&quot;&quot;&quot;
head(s::String) = length(s) == 0 ? &quot;&quot; : join(collect(s)[1:max(1, div(length(s), 2))])

&quot;&quot;&quot;Returns the second half of the string.&quot;&quot;&quot;
tail(s::String) = length(s) == 0 ? &quot;&quot; : join(collect(s)[max(1, div(length(s), 2) + 1):end])

&quot;&quot;&quot;Concatenates two strings.&quot;&quot;&quot;
concat(a::String, b::String) = a * b

&quot;&quot;&quot;Interleaves characters from two strings.&quot;&quot;&quot;
function zip(a::String, b::String)
    total_length = length(a) + length(b)
    result = Vector{Char}(undef, total_length)
    i_a = firstindex(a)
    i_b = firstindex(b)
    i = firstindex(result)
    while i &lt;= total_length
        if i_a &lt;= lastindex(a)
            result[i] = a[i_a]
            i += 1
            i_a = nextind(a, i_a)
        end
        if i_b &lt;= lastindex(b)
            result[i] = b[i_b]
            i += 1
            i_b = nextind(b, i_b)
        end
    end
    return join(result)
end

#=
Now, let&#39;s use these operators to create a dataset.
=#

function single_instance(rng=Random.default_rng())
    x_1 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))
    x_2 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))
    x_3 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))
    x_4 = join(rand(rng, &#39;a&#39;:&#39;z&#39;, rand(rng, 1:10)))

    ## True formula:
    y = zip(x_1 * &quot;abc&quot; * x_2, tail(x_3) * reverse(x_4) * &quot;xyz&quot;)
    return (; X=(; x_1, x_2, x_3, x_4), y)
end

dataset = let rng = Random.MersenneTwister(0)
    [single_instance(rng) for _ in 1:128]
end

#=
We&#39;ll get them in the right format for MLJ:
=#

X = [d.X for d in dataset]
y = [d.y for d in dataset];

#=
To actually get this working with SymbolicRegression,
there are some key functions we will need to overload.

First, we say that a single string is one &quot;scalar&quot; constant:
=#

import DynamicExpressions: count_scalar_constants
count_scalar_constants(::String) = 1

#=
Next, we define an initializer (which is normally 0.0 for numeric types).
=#

import SymbolicRegression: init_value
init_value(::Type{String}) = &quot;&quot;

#=
Next, we define a random sampler. This is only used for
generating initial random leafs; the `mutate_value` function
is used for mutating them and moving around in the search space.
=#

using Random: AbstractRNG
import SymbolicRegression: sample_value
sample_value(rng::AbstractRNG, ::Type{String}, _) = join(rand(rng, &#39;a&#39;:&#39;z&#39;) for _ in 1:rand(rng, 0:5))

#=
We also define a pretty printer for strings,
so it is easier to tell apart variables and operators
from string constants.
=#

import SymbolicRegression.InterfaceDynamicExpressionsModule: string_constant
function string_constant(val::String, ::Val{precision}, _) where {precision}
    val = replace(val, &quot;\&quot;&quot; =&gt; &quot;\\\&quot;&quot;, &quot;\\&quot; =&gt; &quot;\\\\&quot;)
    return &#39;&quot;&#39; * val * &#39;&quot;&#39;
end

#=
We also disable constant optimization for strings,
since it is not really defined. If you have a type that you
do want to optimize, you should follow the `DynamicExpressions`
value interface and define the `get_scalar_constants` and `set_scalar_constants!`
functions.
=#

import SymbolicRegression.ConstantOptimizationModule: can_optimize
can_optimize(::Type{String}, _) = false

#=
Finally, the most complicated overload for `String` is `mutate_value`,
which we need to define so that any constant value can be iteratively mutated
into any other constant value.

We also typically want this to depend on the temperature --- lower temperatures
mean a smaller rate of change. You can use temperature as you see fit, or ignore it.
=#

using SymbolicRegression.UtilsModule: poisson_sample

import SymbolicRegression: mutate_value

sample_alphabet(rng::AbstractRNG) = rand(rng, &#39;a&#39;:&#39;z&#39;)

function mutate_value(rng::AbstractRNG, val::String, T, options)
    max_length = 10
    lambda_max = 5.0
    λ = max(nextfloat(0.0), lambda_max * clamp(float(T), 0, 1))
    n_edits = clamp(poisson_sample(rng, λ), 0, 10)
    chars = collect(val)
    ops = rand(rng, (:insert, :delete, :replace, :swap), n_edits)
    for op in ops
        if op == :insert
            insert!(chars, rand(rng, 0:length(chars)) + 1, sample_alphabet(rng))
        elseif op == :delete &amp;&amp; !isempty(chars)
            deleteat!(chars, rand(rng, eachindex(chars)))
        elseif op == :replace
            if isempty(chars)
                push!(chars, sample_alphabet(rng))
            else
                chars[rand(rng, eachindex(chars))] = sample_alphabet(rng)
            end
        elseif op == :swap &amp;&amp; length(chars) &gt;= 2
            i = rand(rng, 1:(length(chars) - 1))
            chars[i], chars[i + 1] = chars[i + 1], chars[i]
        end
        if length(chars) &gt; max_length
            chars = chars[1:max_length]
        end
    end
    return String(chars[1:min(end, max_length)])
end

#=
This concludes the custom type interface. Now let&#39;s actually use it!

For the loss function, we will use Levenshtein edit distance.
This lets the evolutionary algorithm gradually change the strings
into the desired output.
=#

function edit_distance(a::String, b::String)::Float64
    a, b = length(a) &gt;= length(b) ? (a, b) : (b, a)  ## Want shorter string to be b
    a, b = collect(a), collect(b)  ## Convert to vectors for uniform indexing
    m, n = length(a), length(b)

    m == 0 &amp;&amp; return n
    n == 0 &amp;&amp; return m
    a == b &amp;&amp; return 0

    ## Initialize the previous row (distances from empty string to b[1:j])
    prev = collect(0:n)
    curr = similar(prev)

    for i_a in 1:m
        curr[1] = i_a
        ai = a[i_a]
        for i_b in 1:n
            cost = ai == b[i_b] ? 0 : 1
            curr[i_b + 1] = min(prev[i_b + 1] + 1, curr[i_b] + 1, prev[i_b] + cost)
        end
        prev, curr = curr, prev
    end

    return Float64(prev[n + 1])  ## Make sure to convert to your `loss_type`!
end

#=
Next, let&#39;s create our regressor object. We pass `binary_operators`
and `unary_operators` as normal, but now we also pass `GenericOperatorEnum`,
because we are dealing with non-numeric types.

We also need to manually define the `loss_type`, since it&#39;s not inferrable from
`loss_type`.
=#
binary_operators = (concat, zip)
unary_operators = (head, tail, reverse)
hparams = (;
    batching=true,
    batch_size=32,
    maxsize=20,
    parsimony=0.1,
    adaptive_parsimony_scaling=20.0,
    mutation_weights=MutationWeights(; mutate_constant=1.0),
    early_stop_condition=(l, c) -&gt; l &lt; 1.0 &amp;&amp; c &lt;= 15,  # src
)
model = SRRegressor(;
    binary_operators,
    unary_operators,
    operator_enum_constructor=GenericOperatorEnum,
    elementwise_loss=edit_distance,
    loss_type=Float64,
    hparams...,
);

mach = machine(model, X, y; scitype_check_level=0)

#=
At this point, you would run `fit!(mach)` as usual.
Ignore the MLJ warnings about `scitype`s.
\```julia
fit!(mach)
\```
=#</code></pre><p>which uses Literate.jl to generate this page.</p></details></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../template_parametric_expression/">« Parameterized Template Expressions</a><a class="docs-footer-nextpage" href="../../slurm/">Using SymbolicRegression.jl on a Cluster »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.25 on <span class="colophon-date" title="Monday 28 July 2025 12:56">Monday 28 July 2025</span>. Using Julia version 1.11.6.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
